:Class Client
    ⎕IO←0
    ⎕ML←1

    :Field Public Instance Proc←⍬
    :Field Public Instance Host←''
    :Field Public Instance Port
    :Field Public Instance Messages←⍬
    :Field Private Instance Name←''
    :Field Private Instance HandshakeDone←0
    :Field Private Instance P

    :Field Private Shared MAGIC←82 73 68 69
    :Field Private Shared PORT←4602

    ∇ r←GetPort
      :Access Private Shared
      r←PORT←PORT+1
    ∇

    ∇ make;r
      :Access Public Instance
      :Implements Constructor
      P←(⊃⊃⎕CLASS ⎕THIS).##
      :If 0=P.⎕NC'DRC'
          'DRC'P.⎕CY'conga'
      :EndIf
      r←P.DRC.Init''
      Port←GetPort
    ∇

    ∇ unmake;r
      :Access Private Instance
      :Implements Destructor
      Close
    ∇

    ∇ Close;r
      :Access Public Instance
      :If ~Proc.HasExited
          r←SendMessage'["Exit",{"code":0}]'
          ⎕DL 0.5
          r←P.DRC.Close Name
      :AndIf ~Proc.HasExited
          ⎕←'killing'
          r←Proc.Kill
      :EndIf
    ∇

    ∇ Start;ride
      :Access Public Instance
      ride←'SERVE:',Host,':',⍕Port
      Proc←⎕NEW P.APLProcess('' '' 0 ride)
      Messages←⍬
      ⎕DL 0.5
      r←P.DRC.Clt''Host Port'raw'
      :If 0≠⊃r
          ∘∘∘
      :EndIf
      Name←1⊃r
      HandshakeDone←0
      SendMessage¨'SupportedProtocols=2' 'UsingProtocol=2' '["Identify",{"identity":1}]'
      MessageHandler&⍬
    ∇

    :Section ScriptHelpers

    ∇ {r}←Edit(name body);m1;m2;m3;ns;z;rc
      :Access Public Instance
      z←Execute')ED ',name
      StopIf ¯1=⊃rc m1←WaitFor'OpenWindow'
     
      ns←⎕NS''
      ns.win←m1.token
      ns.text←body
      ns.stop←⍬
      z←SendMessage'SaveChanges'ns
      rc z←WaitFor'TaskDialog' 1000
      :If rc=0
          ns←⎕NS''
          ns.token←z.token
          ns.index←100
          SendMessage'ReplyTaskDialog'ns
      :EndIf
     
      StopIf ¯1=⊃rc m2←WaitFor'ReplySaveChanges'
      StopIf m2.err≠0
     
      ns←⎕NS''
      ns.win←m1.token
      z←SendMessage'CloseWindow'ns
      rc m3←WaitFor'CloseWindow'
      StopIf m3.win≢m1.token
      r←0
      ⍝
    ∇

    ∇ {r}←Execute arg;m;trc;txt
      :Access Public Instance
      txt trc←2↑(⊆arg),0
      m←⎕NS''
      m.text←txt,⎕UCS 10
      m.trace←trc⌷'false' 'true'
      r←SendMessage'Execute'm
    ∇

    ∇ {(rc m)}←WaitFor arg;e;en;i;t
      :Access Public Instance
      e t←2↑(⊆arg),10000
      rc m←¯1 ⍬
      :Repeat
          i←(en←⊃¨Messages)⍳⊂e
          :If i<≢en
              m←i 1⊃Messages
              rc←0
              Messages↓⍨←i+1
          :Else
              t-←1000×⎕DL 0.5⌊t÷1000
          :EndIf
      :Until (t≤0)∨(rc=0)
    ∇

    ∇ {r}←WaitForPromptType arg;done;m;timeout;type;start;rc;rem
      :Access Public Instance
      type timeout←2↑arg,(≢arg)↓1 10000
      r←¯1 ⍬
      done←0
      start←2⊃⎕AI
      rem←timeout
      :Repeat
          rc m←WaitFor'SetPromptType'rem
          :If rc=0
              done←m.type=type
          :EndIf
          rem←timeout-(2⊃⎕AI)-start
      :Until (rem<0)∨done
      :If done
          r←0 m
      :EndIf
    ∇

    ∇ {r}←WaitForSessionOutput timeout;e;en;i;m;rc
      :Access Public Instance
      r←''
      :Repeat
          rc m←WaitFor'AppendSessionOutput'timeout
          :If rc=0
              timeout←0
              r,←m.result
          :EndIf
      :Until rc=¯1
    ∇
    
    :EndSection

    ∇ {r}←MessageHandler b;dat;done;evt;msgs;nm;ok;pmsgs;rc
      done←0
      :Repeat
          r←P.DRC.Wait Name
          rc nm evt dat←4↑r
          evt←(rc=100)⊃evt'Timeout'
          :Select evt
          :Case 'BlockLast'
              r←P.DRC.Close Name
          :Case 'Timeout'
     
          :Case 'Block'
              b,←dat
              b msgs←ParseMessage b
              pmsgs←⎕JSON⍣HandshakeDone¨msgs
              Messages,←pmsgs
              :If ~HandshakeDone
              :AndIf 2≤≢Messages
                  ok←∧/'SupportedProtocols=2' 'UsingProtocol=2'∊2↑Messages
                  HandshakeDone←ok
                  Messages↓⍨←2
                  done←~ok
              :EndIf
          :Else
              done←1
          :EndSelect
      :Until done
    ∇

      ParseMessage←{
          ⍺←⍬
          0=l←256⊥4↑⍵:⍵ ⍺
          l>≢⍵:⍵ ⍺
          MAGIC≢4↑4↓⍵:∘∘∘ ⍝ RIDE magic number
          json←8↓l↑⍵
          msgs←⍺,⊂'UTF-8'⎕UCS json
          msgs ∇ l↓⍵
      }

    ∇ {r}←SendMessage m;array;d;p
      array←~(1=≢⍴m)∧(0=10|⎕DR m)
      p←'UTF-8'⎕UCS ⎕JSON⍣array⊢m
      d←{⍵-256×128<⍵}(4/256)⊤8+≢p
      r←P.DRC.Send Name(d,MAGIC,p)
    ∇

      StopIf←{
          ⍵:r←(1+1⊃⎕LC)⎕STOP 1⊃⎕SI
      }

:EndClass
